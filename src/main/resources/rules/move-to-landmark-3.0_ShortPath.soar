# This is similar to move-to-landmark-3.0 but the main difference is the usage of computed shortest path of landmarks

sp {initialize*state*preferable*directions
   (state <ss> ^type state)
   -->
   (<ss> ^preferable-directions <ne> <nw> <sw> <se> ) 
   
   (<ne> ^value northeast ^prefer northeast) 
   (<ne> ^value northeast ^prefer east)
   (<ne> ^value northeast ^prefer north)
   
   (<nw> ^value northwest ^prefer northwest)
   (<nw> ^value northwest ^prefer west)
   (<nw> ^value northwest ^prefer north)

   (<sw> ^value southwest ^prefer southwest)
   (<sw> ^value southwest ^prefer south)
   (<sw> ^value southwest ^prefer west)

   (<se> ^value southeast ^prefer southeast)
   (<se> ^value southeast ^prefer south)
   (<se> ^value southeast ^prefer east)
}

#
#START: Proposal Rules
#
#

sp {propose*move-to-landmark
   (state <s> ^io.input-link.landmarks.<landmarks> <landmark>     
              ^io.input-link.self.pose <e>
              ^io.input-link.area.<view> <area-view>
              ^io.input-link.landmark-cycle-count <landmark-cycle-count> )
   (<e> ^x <x> ^y <y>)
   (<landmark> ^path <shortest-path> )
   (<landmark> ^name <landmark-name> )
   (<shortest-path> ^<landmark-cycle-count> <computed-path>)

   (<area-view> ^<computed-path>.type <cell-type>)
-->
# (write (crlf) |<move-index>-| <move-index> (crlf) | : |  <landmarks>)
   (<s> ^operator <o> + = )
   (<o> ^name move-to-landmark
        ^landmark <landmark>
        ^direction-command <computed-path>
        ^path-size (size <shortest-path> ) 
        ^cell-type <cell-type>)}


sp {propose*move-random
   (state <s> ^io.input-link.self.pose <e>
              ^io.input-link.area.<view> <v-directions> 
              ^io.input-link.landmarks.status 
               { <landmarks-status> <> inactive } )
    (<e> ^x <x> ^y <y>)
    (<v-directions> ^ { << north northeast northwest east west south southeast southwest >> <n-dir> } )
    (<v-directions> ^<n-dir>.type <> block )
    
-->
   (<s> ^operator <o> + =)
   (<o> ^name move-random
        ^direction-command <n-dir> )}


sp {apply*move
   (state <s> ^io.output-link <ol>
              ^operator <o>)
   (<o> ^name { <name> << move-to-landmark move-random >> }
        ^direction-command <dir>)
-->
   (<ol> ^move.direction <dir>)} 


sp {apply*move*remove-move
   (state <s> ^io.output-link <ol>
              ^operator.name <name>)
   (<ol> ^move <direction>)
   (<direction> ^status complete)
-->
   (<ol> ^move <direction> -)}

sp {apply*move*create*current-landmark-name
   (state <s> ^operator <o>)
   (<o> ^name { <name> move-to-landmark }
        ^landmark.name <landmark-name>)
-->
   (<s> ^current-landmark-name <landmark-name>)}

sp {apply*move*create*last-direction
   (state <s> ^operator <o>)
   (<o> ^name { <name> << move-to-landmark move-random >> }
        ^direction-command <direction>)
-->
   (<s> ^last-direction <direction>)}


sp {apply*move*remove*last-direction
   (state <s> ^operator <o>
              ^last-direction <direction>)
   (<o> ^name { <name> << move-to-landmark move-random >> }
        ^direction-command <> <direction>)
-->
#    (<s> ^prior-direction <direction> )
   (<s> ^last-direction <direction> -)}  


#
#END: Apply Rules
#
#

#
#START: Select Rules
#
#

sp {select*move-to-landmark*pick-landmark-with-shortest-distance
    (state <s> ^operator <o1> + 
                ^operator <o2> + )
    (<o1> ^name move-to-landmark
            ^path-size <path-size1> )
    (<o2> ^name move-to-landmark
            ^path-size  <path-size2> > <path-size1> )
-->
(<s> ^operator <o1> > <o2>)}

sp {select*move-to-landmark*pick-current-landmark-if-shortest-distance-is-same
    (state <s> ^operator <o1> + 
                ^operator <o2> +
                ^current-landmark-name <current-landmark-name> )
    (<o1> ^name move-to-landmark
            ^path-size <path-size1>
            ^landmark.name <current-landmark-name> )
    (<o2> ^name move-to-landmark
            ^path-size  <path-size2> <path-size1>
            ^landmark.name <other-landmark-name> )
-->
(<s> ^operator <o1> > <o2>)}

sp {select*move-to-landmark*pick-other-direction-if-block
    (state <s> ^operator <o1> +
                ^operator <o2> + )
    (<o1> ^name move-random )
    (<o2> ^name move-to-landmark
            ^cell-type << block >> )
    
-->
(<s> ^operator <o1> > <o2> )}

sp {select*move-to-landmark*pick-original-landmark-direction
    (state <s> ^operator <o1> + )
    (<o1> ^name move-to-landmark 
        ^cell-type << normal >> )
-->
(<s> ^operator <o1> > )}


sp {select*move-to-landmark*pick-prefer-directions-over-others-if-landmark-block
    (state <s> ^operator <o1> + 
                ^operator <o2> +
                ^preferable-directions <pd>
                ^last-direction <l-dir> )
    (<pd> ^value <l-dir>
        ^prefer <p-dir> )
    (<o1> ^name move-random
            ^direction-command <p-dir> )
    (<o2> ^name move-to-landmark
            ^cell-type << block >>  )
    
-->
(<s> ^operator <o1> > )}

sp {select*move-forward*prefer-forward-than-others
    (state <s> ^operator <o1> +
                ^operator <o2> +
                ^last-direction <lastdirection> )
    (<o1> ^name << move-random >>
            ^direction-command <lastdirection> )
    (<o2> ^name << move-random >>
            ^direction-command <direction> )
-->
(<s> ^operator <o1> > <o2>)}