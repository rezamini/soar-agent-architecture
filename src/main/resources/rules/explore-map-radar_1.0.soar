sp {propose*move
   (state <s> ^io.input-link.area.view <area-view>
              ^io.input-link.area.locale.direction <current-direction>
              ^io.input-link.self.pose <e> )
   (<area-view> ^<current-direction>.type <> block )
   (<e> ^x <x> ^y <y>)
-->
# (write | proposing move : | <direction-command> )
   (<s> ^operator <o> + =)
   (<o> ^name move
        ^actions.move.direction <current-direction> )}


 sp {propose*turn
   (state <s> ^io.input-link.area.view <area-view>
              ^io.input-link.area.locale.direction <current-direction>
              ^io.input-link.self.pose <e> )
   (<area-view> ^<current-direction>.type block
                ^<turn-direction>.type normal )

    # (<area-view> ^ { << north northeast northwest east west south southeast southwest >> <n-dir> } )
    # (<area-view> ^<n-dir>.type <> block )
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn 
        ^actions <a>)
   (<a> ^move.direction <turn-direction>
        ^radar.toggleRadar on )}


sp {propose*radar-off
   (state <s> ^io.input-link.radar <radar>)
   (<radar> ^status on
         ^battery < 80)
        #  ^radar <r>)
#   -(<r> ^ << energy health missiles tank >> <x>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name radar-off
        ^actions.radar.toggleRadar off )}


## Selection rules

sp {select*radar-off*move
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name radar-off)
   (<o2> ^name move)
-->
   (<s> ^operator <o1> > <o2>)}

sp {apply*operator*create-action-command
   (state <s> ^operator <o>
              ^io.output-link <ol>)
   (<o> ^actions <act>)
   (<act> ^<att> <value>)
-->
   (<ol> ^<att> <value>)}

sp {apply*operator*remove-command
   (state <s> ^operator.actions
              ^io.output-link <ol>)
   (<ol> ^<att> <value>)
   (<value> ^status complete)
-->
   (<ol> ^<att> <value> -)}

